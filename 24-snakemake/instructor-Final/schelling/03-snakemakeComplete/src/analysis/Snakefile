# Subworkflow: Analysis
# Main Contributor: Lachlan Deer (@ldeer)

import glob, os

# --- Importing Configuration Files --- #

configfile: "config.yaml"

# --- Set up Dictionaries --- #

# Get a list of all models we want to run
MODELS = [os.path.splitext(os.path.basename(textNames))[0]
                for textNames in glob.glob(config["src_model_specs"] + "*.json")]

# --- Sub Workflows --- #
subworkflow data_mgt:
    workdir: "../../" + config["ROOT"]
    snakefile: "../../" + config["src_data_mgt"] + "Snakefile"

# --- Build Rules --- #

## models  : run the Schelling model using the baseline rules on how
##           agents can move
rule models:
    input:
        expand(config["out_analysis"] + "{iModel}.pickle", iModel = MODELS)

## simulate_model: simulates a model specified in `models` rule
# Pass variables to shell in the following order
#     (0) The script to run
#     (1) Model Specification
#     (2) The Data Set
#     (3) Location of the Log file
#     (4) The pickle file that stores the output

rule simulate_model:
    input:
        data        = data_mgt(config["out_data"] + "initial_locations.csv"),
        script      = config["src_analysis"] + "schelling.py",
        model_rules = config["src_model_specs"] + "{model}.json",
        model_code  = config["src_model_code"] + "agent.py"
    output:
        data = config["out_analysis"] + "{model}.pickle",
        log  = config["out_analysis"] + "log/{model}.log"
    shell:
        "python {input.script} {input.model_rules} {input.data} \
        	   {output.log} {output.data}"
